plugins {
    id "base"
    id "idea"
    id "com.github.node-gradle.node" version "3.0.1"
}

node {
    version = '12.15.0'
    workDir = file("${project.buildDir}/nodejs")
    yarnVersion = '1.22.5'
    download = true
}

task yarnInstall(type: YarnTask) {
    inputs.file 'package.json'
    inputs.file 'yarn.lock'
    outputs.dir 'node_modules'
    args = ['install', '--pure-lockfile']
}

task installDependencies(dependsOn: yarnInstall) {
  description = 'Install all frontend dependencies'
}

task buildProduction(type: YarnTask, dependsOn: installDependencies) {
    inputs.dir 'src'
    inputs.files 'package.json', 'yarn.lock', 'angular-cli.json'
    outputs.dir 'dist'
    args = ['run', 'buildProduction']
}

task test(type: YarnTask, dependsOn: installDependencies) {
  args = ['run', 'e2e']
}

task serve(type: YarnTask, dependsOn: installDependencies) {
  args = ['run', 'start']
}

task deleteNodeModules(type: Delete) {
    delete 'node_modules'
}

task frontendJar(type: Jar, dependsOn: buildProduction) {
    baseName "rhp-frontend"
    from "dist"
    eachFile { details ->
        details.path = "static/${details.path}"
    }
    // Jar contains duplicate empty folders, see Gradle issue:
    // http://issues.gradle.org/browse/GRADLE-1830
    // so we need to set includeEmptyDirs to false
    includeEmptyDirs = false
}

artifacts {
    archives frontendJar
}
